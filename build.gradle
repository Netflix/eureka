buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}

plugins {
    id 'nebula.netflixoss' version '2.2.10'
}

ext {
    githubProjectName = 'eureka'
    awsVersion='1.9.3'
    servletVersion='2.5'
    jerseyVersion='1.19'
    jettisonVersion='1.3.7'
    apacheHttpClientVersion='4.3.4'
    guiceVersion='4.0'
    servoVersion='0.12.3'
    governatorVersion='1.12.5'
    archaiusVersion='0.7.3'
    archaius2Version='2.0.0'
    blitzVersion='1.34'
    mockitoVersion='1.10.19'
    junit_version='4.11'
    mockserverVersion='3.9.2'
    jetty_version='7.2.0.v20101020'
    jacksonVersion='2.5.4'
    woodstoxVersion='4.4.1'
    karyon3Version='3.0.1-rc.23'
}

idea {
    project {
        languageLevel = '1.7'
    }
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }

    test {
        forkEvery = 1
        // setting this property prevents java from grabbing focus when running the tests under osx
        systemProperty 'java.awt.headless', 'true'
    }

    jar {
        manifest {
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }
}

