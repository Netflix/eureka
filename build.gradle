buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:8.+'
    }
}

plugins {
    id 'com.netflix.nebula.netflixoss' version '11.1.1'
    id 'org.gretty' version '4.0.3'
}

idea {
    project {
        languageLevel = '1.8'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

allprojects {
    ext {
        githubProjectName = 'eureka'

        awsVersion = '1.11.277'
        servletVersion = '5.0.0'
        jettisonVersion = '1.5.4'
        apacheHttpClientVersion = '4.5.3'
        commonsConfigurationVersion = '1.10'
        jsr305Version = '3.0.2'
        servoVersion = '0.12.21'
        archaiusVersion = '0.7.6'
        jacksonVersion = '2.10.5'
        jacksonDatabindVersion = '2.10.5.1'
        woodstoxVersion = '5.2.1'

        // test deps
        jetty_version = '11.0.11'
        junit_version = '4.11'
        mockitoVersion = '4.6.1'
        mockserverVersion = '3.9.2'
    }
}

subprojects {
    apply plugin: 'com.netflix.nebula.netflixoss'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    test {
        forkEvery = 1
        // setting this property prevents java from grabbing focus when running the tests under osx
        systemProperty 'java.awt.headless', 'true'
    }

    jar {
        manifest {
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }
}
