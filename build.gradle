ext.githubProjectName = 'eureka'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { 
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
    group = "com.netflix.${githubProjectName}"
    jar {
        manifest {
            attributes("Implementation-Title": name, "Implementation-Version": version,'Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }
}


project(':eureka-client') {
    dependencies {
        compile 'com.thoughtworks.xstream:xstream:1.4.2'
        compile 'com.netflix.archaius:archaius-core:0.3.3'
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        compile 'com.netflix.servo:servo-core:0.4.36'
        compile 'com.sun.jersey:jersey-bundle:1.9.1'
        compile 'com.sun.jersey.contribs:jersey-apache-client4:1.8'
        compile 'org.apache.httpcomponents:httpclient:4.1.2'
        runtime 'org.codehaus.jettison:jettison:1.2'

        testCompile 'junit:junit:4.10'
        testCompile 'org.mortbay.jetty:jetty:6.1H.22'
    }
}

project(':eureka-core') {
    dependencies {
        compile project(':eureka-client')
        compile 'com.amazonaws:aws-java-sdk:1.3.27'
        compile 'javax.servlet:servlet-api:2.4'
        compile 'com.thoughtworks.xstream:xstream:1.4.2'
        compile 'com.netflix.archaius:archaius-core:0.3.3'
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        compile 'com.netflix.servo:servo-core:0.4.36'
        compile 'com.sun.jersey:jersey-bundle:1.9.1'
        compile 'com.sun.jersey.contribs:jersey-apache-client4:1.8'
        compile 'org.apache.httpcomponents:httpclient:4.1.2'
        compile 'com.netflix.blitz4j:blitz4j:1.18'

        testCompile 'junit:junit:4.10'
        testCompile 'org.mortbay.jetty:jetty:6.1H.22'
        testRuntime 'org.slf4j:slf4j-simple:1.7.0'
    }
}

project(':eureka-resources') {
}


project(':eureka-server') {
    apply plugin: 'war'

    sourceSets.main.java.srcDir 'test/java'
  
    war {
        from('conf')  {
            include 'eureka-server*'
            include 'eureka-client*'
            include 'log4j.properties'
            into 'WEB-INF/classes/'
        }
        from (project(':eureka-resources').file('build/resources/main'))
        webXml = file('WEB-INF/web.xml') // copies a file to WEB-INF/web.xml
    }
	
    dependencies {
        compile project(':eureka-client')
        compile project(':eureka-core')
        runtime 'xerces:xercesImpl:2.4.0'
        runtime 'asm:asm:3.1' 
        compile 'org.slf4j:slf4j-log4j12:1.6.1'
        runtime 'org.codehaus.jettison:jettison:1.2' 
        providedCompile 'javax.servlet:servlet-api:2.4'
    }

    delete 'testlibs'
    copy { 
        from zipTree('build/libs/'+ war.archiveName)
        into 'testlibs' 
        include 'WEB-INF/lib/'
    }
}

