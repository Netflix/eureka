buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}

plugins {
    id 'nebula.netflixoss' version '9.1.0'
    id 'org.gretty' version '2.1.0'
}

idea {
    project {
        languageLevel = '1.8'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

allprojects {
    ext {
        githubProjectName = 'eureka'

        awsVersion = '1.11.277'
        servletVersion = '2.5'
        jerseyVersion = '1.19.1'
        jettisonVersion = '1.4.0'
        apacheHttpClientVersion = '4.5.3'
        commonsConfigurationVersion = '1.10'
        jsr305Version = '3.0.2'
        guiceVersion = '4.1.0'
        servoVersion = '0.12.21'
        governatorVersion = '1.17.5'
        archaiusVersion = '0.7.6'
        jacksonVersion = '2.9.10'
        jacksonDatabindVersion = '2.9.10.6'
        woodstoxVersion = '5.2.1'

        // test deps
        jetty_version = '7.2.0.v20101020'
        junit_version = '4.11'
        mockitoVersion = '1.10.19'
        mockserverVersion = '3.9.2'
    }
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    test {
        forkEvery = 1
        // setting this property prevents java from grabbing focus when running the tests under osx
        systemProperty 'java.awt.headless', 'true'
    }

    jar {
        manifest {
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }
}
